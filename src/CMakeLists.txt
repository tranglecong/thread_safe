
set(THREAD_SAFE_HEADER_PATH "${THREAD_SAFE_SOURCE_DIR}/include/thread_safe")
file(GLOB THREAD_SAFE_HEADER_FILES "${THREAD_SAFE_SOURCE_DIR}/include/thread_safe/*.hpp")
file(GLOB THREAD_SAFE_SOURCES_FILES "${THREAD_SAFE_SOURCE_DIR}/src/*.cpp")

add_library(thread_safe ${THREAD_SAFE_HEADER_FILES} ${THREAD_SAFE_SOURCES_FILES})
target_include_directories(thread_safe PUBLIC $<BUILD_INTERFACE:${THREAD_SAFE_SOURCE_DIR}/include/> # For client in subdirectory
    $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}> # For generated files in build mode
    $<INSTALL_INTERFACE:${THREAD_SAFE_INC_INSTALL_DIR}> # For client in install mode
    PRIVATE ${THREAD_SAFE_HEADER_PATH}) # For source files

target_compile_definitions(thread_safe PUBLIC
    THREAD_SAFE=1
    THREAD_SAFE_VERSION_MAJOR=${THREAD_SAFE_VERSION_MAJOR}
    THREAD_SAFE_VERSION_MINOR=${THREAD_SAFE_VERSION_MINOR}
    THREAD_SAFE_VERSION_PATCH=${THREAD_SAFE_VERSION_PATCH})

set_target_properties(thread_safe PROPERTIES
    OUTPUT_NAME "thread_safe-${THREAD_SAFE_VERSION}"
    POSITION_INDEPENDENT_CODE ON)

install(TARGETS thread_safe EXPORT thread_safeTargets
    RUNTIME DESTINATION ${THREAD_SAFE_RUNTIME_INSTALL_DIR}
    LIBRARY DESTINATION ${THREAD_SAFE_LIBRARY_INSTALL_DIR}
    ARCHIVE DESTINATION ${THREAD_SAFE_ARCHIVE_INSTALL_DIR}
    FRAMEWORK DESTINATION ${THREAD_SAFE_FRAMEWORK_INSTALL_DIR})

# Write/install version file
include(CMakePackageConfigHelpers)
set(THREAD_SAFE_VERSION_FILE "${CMAKE_CURRENT_BINARY_DIR}/cmake/thread_safe-config-version.cmake")
write_basic_package_version_file(${THREAD_SAFE_VERSION_FILE}
    VERSION ${THREAD_SAFE_VERSION}
    COMPATIBILITY AnyNewerVersion)

install(FILES ${THREAD_SAFE_HEADER_FILES} DESTINATION ${THREAD_SAFE_INC_INSTALL_DIR}/thread_safe)
install(FILES ${THREAD_SAFE_VERSION_FILE} DESTINATION ${THREAD_SAFE_CMAKE_CONFIG_INSTALL_DIR})